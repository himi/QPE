grammar org.omg.qpe.QPE with org.eclipse.xtext.common.Terminals

import "http://www.omg.org/spec/SST/QPE/1.0"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

QPE:
        queryNamespaces += QueryNamespace
        pathExpressions += PathExpression
;

QueryNamespace:
        'namespace' IRI=STRING ('as' prefix=ID)?      
;

PathExpression: {PathExpression}
	(isRelative?='.')? (qualifier=Qualifier)? '/' (head=QueryElement)?
;

QueryElement:
	( queryNamespace=[QueryNamespace] '::' )? feature=[ecore::EStructuralFeature] /*CustomScope*/
	( qualifier=Qualifier )?
	( '/' next=QueryElement )?
;

Qualifier:
	'[' 
	    ((index=INT (',' predicates += Predicate)*) |
	     ((predicates += Predicate) (',' predicates += Predicate)*))
	']'
;

Predicate:
	ClassifierPredicate | AttributePredicate | ReferencePredicate	
;

ClassifierPredicate:
	( queryNamespace=[QueryNamespace] /*DefaultScope*/ '::' )? classifier=[ecore::EClassifier] /*CustomScope*/
;

AttributePredicate:
	( queryNamespace=[QueryNamespace] /*DefaultScope*/ '::' )? attribute=[ecore::EAttribute] '=' /*CustomScope*/ value=VALUE
;

ReferencePredicate:
	( queryNamespace=[QueryNamespace] /*DefaultScope*/ '::' )? reference=[ecore::EReference] /*CustomScope*/ qualifier=Qualifier
;

/* terminal */ VALUE /* returns ecore::EJavaObject */: STRING | INT | LONG | DOUBLE | BOOLEAN;

terminal LONG returns ecore::ELong: INT ("L"|"l");
terminal DOUBLE returns ecore::EDouble: INT'.'INT;
terminal BOOLEAN returns ecore::EBoolean: "true" | "false";


